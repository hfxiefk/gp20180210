http://www.55188.com/viewthread.php?tid=3373436&extra=&highlight=%D6%F7%C1%A6%C0%D7%B4%EF&page=1
1、该指标为同花顺加密当家公式"主力雷达"的TDX版,并在原来的基础上对信号做了些改进;现发上来让大家分享!
2、图中蓝线代表散户监控线，红线主力表示监控线；
 当红线自下而上上穿蓝线时，表明市场场内资金做多力量逐渐增加，此时可适当关注；
当红线自上而下下穿蓝线时，表明市场场内资金做空力量逐渐增加，此时应注意市场风险。
当图中提示“顶”时，需要注意市场风险；
当图中提示“底”时，表明股价调整幅度相对较大，有反弹需求；当图中在同一天显示
“底”和“升”时，个股短线见底概率较大。
当图中提示“升”时，表明股价已有极大幅度的调整,接下来涨升的可能性较大；
当出现"金底"信号时买入,后市涨升的概率极大;
	
{主力雷达附图}
LC:=REF(CLOSE,1);
RSI1:=SMA(MAX(CLOSE-LC,0),6,1)/SMA(ABS(CLOSE-LC),6,1)*100;
AR:=SUM(HIGH-OPEN,26)/SUM(OPEN-LOW,26)*100;
卖点雷达:=CROSS(85,RSI1)*30,COLOR00FF00,LINETHICK2;
DRAWTEXT(CROSS(85,RSI1),20,'顶'),COLOR00FF00;
STICKLINE(卖点雷达,0,20,3,0),COLOR00FF00; 
VARB:=SMA(MAX(CLOSE-LC,0),7,1)/SMA(ABS(CLOSE-LC),7,1)*100; 
VARC:=SMA(MAX(CLOSE-LC,0),13,1)/SMA(ABS(CLOSE-LC),13,1)*100; 
VARD:=BARSCOUNT(CLOSE); 
买点雷达:(VARB< 20 AND VARC< 25 AND VARD> 50 AND AR<70)*30,LINETHICK2,COLOR0078FF;
BT1:=CROSS(RSI1,25);
DRAWTEXT(BT1,-80,'底'),COLOR0000FF;
STICKLINE(BT1,-100,-80,3,0),COLOR0000FF; 
主力:EMA( (CLOSE-MA(CLOSE,7))/MA(CLOSE,7)*480,2)*5,LINETHICK1,COLOR0000FF; 
散户:EMA( (CLOSE-MA(CLOSE,11))/MA(CLOSE,11)*480,7)*5,LINETHICK2,COLORC87800; 
BT2:=CROSS(主力,散户) AND 主力<-10;   
DRAWICON(CROSS(RSI1,25),主力,26);
DRAWICON(CROSS(主力,散户) AND 主力<-10,主力,23);
BT:=FILTER(COUNT(BT1 OR BT2,3)>=2,3);
STICKLINE(BT,0,50,4,1),COLOR0000FF; 
DRAWTEXT(BT,50,'金底'),COLOR0000FF; 
STICKLINE(BT2,0,20,3,0),COLORFF00FF;
DRAWTEXT(BT2,20,'升'),COLORFF00FF;


选股公式原码:
LC:=REF(CLOSE,1);
RSI1:=SMA(MAX(CLOSE-LC,0),6,1)/SMA(ABS(CLOSE-LC),6,1)*100;
AR:=SUM(HIGH-OPEN,26)/SUM(OPEN-LOW,26)*100;
VARB:=SMA(MAX(CLOSE-LC,0),7,1)/SMA(ABS(CLOSE-LC),7,1)*100; 
VARC:=SMA(MAX(CLOSE-LC,0),13,1)/SMA(ABS(CLOSE-LC),13,1)*100; 
VARD:=BARSCOUNT(CLOSE); 
买点雷达:=(VARB< 20 AND VARC< 25 AND VARD> 50 AND AR<70 AND VOL<REF(VOL,1) 
AND REF(VOL,1)<REF(VOL,2) AND REF(VOL,2)<REF(VOL,3))*30;
TJ1:=CROSS(RSI1,25);
主力:=EMA( (CLOSE-MA(CLOSE,7))/MA(CLOSE,7)*480,2)*5,LINETHICK2,COLOR0000FF; 
散户:=EMA( (CLOSE-MA(CLOSE,11))/MA(CLOSE,11)*480,7)*5,LINETHICK2,COLOR00FF00; 
TJ2:=CROSS(主力,散户) AND 主力<-10;   
TJ:=FILTER(COUNT(TJ1 OR TJ2,2)=2,5);
XG:TJ OR TJ1 OR TJ2;