RSI做为一款经典指标，已经被人们广泛使用。我也特别喜欢用，在719行情纪念日快临之际，为预祝下一个牛市底部的站稳，出现未来五年的大牛市，特将我的RSI战神拿出，祝大家赚得盆满罅满。

1.RSI>80 为超买，RSI<20 为超卖；
2.RSI 以50为中界线，大于50视为多头行情，小于50视为空头行情；
3.RSI 在80以上形成Ｍ头或头肩顶形态时，视为向下反转信号；
4.RSI 在20以下形成Ｗ底或头肩底形态时，视为向上反转信号；

红方块为BIAS买入；
红色向上箭头为RSI买入；
绿色向下箭头为RSI卖出；
黄色向上三角▲为突破上轨；
黄色向下三角▼为跌破下轨；
抄为抄底，逃为逃顶；
白色向上三角 ▲为>昨日RSI1时的指示，表示趋势向上；
白色向下三角▼为<昨日RSI1时的指示，表示趋势向下；

P  2 100 5  N1 2 100 6  HIGHN 2 100 20
LC:=REF(CLOSE,1);
RSI1:SMA(MAX(CLOSE-LC,0),6,1)/SMA(ABS(CLOSE-LC),6,1)*100,COLORWHITE;
AA:=REF(RSI1,HIGHN)=HHV(RSI1,2*HIGHN+1);
QY:=BACKSET(AA,HIGHN+1);
CC:=FILTER(QY,HIGHN) AND RSI1=HHV(RSI1,HIGHN+1);
DDD:=BARSLAST(CC);
GDTS:=IF(CC,CURRBARSCOUNT,0);
DINGBUTS:=IF(DDD,REF(GDTS,DDD),GDTS);
GDCS1:=CONST(DINGBUTS);
DINGBUTS1:=REF(DINGBUTS,DDD+1);
GDCS2:=CONST(DINGBUTS1);
AA1:=REF(RSI1,HIGHN)=LLV(RSI1,2*HIGHN+1);
QY1:=BACKSET(AA1,HIGHN+1);
CC1:=FILTER(QY1,HIGHN) AND RSI1=LLV(RSI1,HIGHN+1);
DD1:=BARSLAST(CC1);
DDTS:=IF(CC1,CURRBARSCOUNT,0);
DDTS1:=IF(DD1,REF(DDTS,DD1),DDTS);
DDCS1:=CONST(DDTS1);
DDTS2:=REF(DDTS1,DD1+1);
DDCS2:=CONST(DDTS2);
A1:=REF(RSI1,P)=HHV(RSI1,2*P+1);
B1:=FILTER(A1,P);
C1:=BACKSET(B1,P+1);
D1:=FILTER(C1,P);
A2:=REF(RSI1,P)=LLV(RSI1,2*P+1);
B2:=FILTER(A2,P);
C2:=BACKSET(B2,P+1);
D2:=FILTER(C2,P);
R11:=BACKSET(ISLASTBAR,BARSLAST(D1)+1);
S11:=R11>REF(R11,1);
T11:=BACKSET(ISLASTBAR,BARSLAST(D2)+1);
U11:=T11>REF(T11,1);
MID:=MA(RSI1,20);
UPPER:=MID + 2*STD(RSI1,20);
LOWER:=MID - 2*STD(RSI1,20);
高:=REF(HHV(RSI1,360),1);
低:=REF(LLV(RSI1,240),1);
BIAS1:=(C-MA(C,6))/MA(C,6)*100;
BIAS2:=(C-MA(C,12))/MA(C,12)*100;
BIAS3:=(C-MA(C,24))/MA(C,24)*100;
BIAS:=(BIAS1+2*BIAS2+3*BIAS3)/6;
乖离:=MA(BIAS,6);
前高:REF(RSI1,BARSLAST(D1)),COLORFF00FF,NODRAW;
前低:REF(RSI1,BARSLAST(D2)),COLOR00FF00,NODRAW;
相对能量:(RSI1-前低)/(前高-前低)*100,COLORYELLOW,NODRAW; 

上轨:UPPER,COLOR993300;
下轨:LOWER,COLOR993300;
DRAWICON(CC,RSI1*1.03,2);
DRAWICON(CC1,RSI1*0.97,1);  
高压:DRAWLINE(CURRBARSCOUNT=GDCS2,RSI1,CURRBARSCOUNT=GDCS1,RSI1,1) COLORBLUE,POINTDOT,LINETHICK4;
低支:DRAWLINE(CURRBARSCOUNT=DDCS2,RSI1,CURRBARSCOUNT=DDCS1,RSI1,1) COLORRED,POINTDOT,LINETHICK4;
阻力:DRAWLINE(D1,RSI1,S11,RSI1,1),COLORYELLOW; 
支撑:DRAWLINE(D2,RSI1,U11,RSI1,1),COLOR00FF00; 
DRAWTEXT(CROSS(RSI1,LOWER),LOWER,'抄'),COLOR00FF00;
DRAWTEXT(CROSS(LOWER,RSI1),LOWER,'▼'),COLOR00FF00;
DRAWTEXT(CROSS(RSI1,高),MID,'年高'),COLORYELLOW;
DRAWTEXT(CROSS(低,RSI1),MID,'年低'),COLORYELLOW;
DRAWTEXT(CROSS(UPPER,RSI1),UPPER,'逃'),COLORYELLOW;
DRAWTEXT(CROSS(RSI1,UPPER),UPPER,'▲'),COLORYELLOW;
VM:MA(RSI1,N1),COLORFF00FF,NODRAW;
STICKLINE(CROSS(BIAS,乖离)  AND 乖离<-9,6,0,2,0),COLORRED;
DRAWTEXT(ISLASTBAR AND RSI1>REF(RSI1,1),VM,' ▲'),COLORDD00FF;
DRAWTEXT(ISLASTBAR AND RSI1<REF(RSI1,1),VM,' ▼'),COLORCCEE00;
DRAWTEXT(CURRBARSCOUNT=60,10,' ☆  东家指标  ☆'),COLORGREEN; 

东方家专辑
http://www.55188.com/thread-3405736-1-1.html

怕未来函数的可删除下面几句，这几句我只是作参考，只显数据，不显图。　
前高:REF(RSI1,BARSLAST(D1)),COLORFF00FF,NODRAW;
前低:REF(RSI1,BARSLAST(D2)),COLOR00FF00,NODRAW;
相对能量:(RSI1-前低)/(前高-前低)*100,COLORYELLOW,NODRAW;