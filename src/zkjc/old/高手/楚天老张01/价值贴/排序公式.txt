http://www2.55188.com/viewthread.php?tid=8195605&extra=&page=8
排序公式

成本线:=COSTEX(HHV(H,SUMBARS(VOL,CAPITAL)-1),LLV(L,SUMBARS(VOL,CAPITAL)-1)),COLORBROWN,LINETHICK2;

股本线:=REF(IF(CLOSE>=OPEN,LOW,HIGH),
BARSLAST(BACKSET(ISLASTBAR,CONST(SUMBARS(VOL,CAPITAL))) 
AND REF(BACKSET(ISLASTBAR,CONST(SUMBARS(VOL,CAPITAL))),1)=0)),COLORFFAAFF,LINETHICK2;

N:=3;
阴K距离:=SUMBARS(CLOSE<OPEN,N)-1,NODRAW;
阳K距离:=SUMBARS(CLOSE>OPEN,N)-1,NODRAW;
阴前高:=IF(CURRBARSCOUNT<=CONST(阴K距离)+1,CONST(REF(HIGH,阴K距离)),DRAWNULL),COLORMAGENTA,DOTLINE;
阳前高:=IF(CURRBARSCOUNT<=CONST(阳K距离)+1,CONST(REF(H,阳K距离)),DRAWNULL),COLORGREEN,DOTLINE;

均线:=DKX.DKX,COLORWHITE;
MN:=MA(DKX.DKX,6),COLORYELLOW;

TJG:=NOT(NAMELIKE('S') OR NAMELIKE('*S')) AND DYNAINFO(8)>0 AND BARSCOUNT(C)>20;

TP1:=CROSS(C,阴前高);
TP2:=CROSS(C,阳前高) AND REF(C<O,1);
TP3:=CROSS(C,股本线);
TPX:=TP1 OR TP2 OR TP3;


XG1:TPX AND C>股本线 AND C>MAX(REF(H,阴K距离),REF(H,阳K距离)) AND C>MAX(均线,MN) AND TJG;
XG2:XG1 AND C>成本线;

PX1:IF(XG1>0,SUMBARS(V,CAPITAL),DRAWNULL),COLORWHITE,NODRAW;
PX2:IF(XG2>0,SUMBARS(V,CAPITAL),DRAWNULL),COLORWHITE,NODRAW;

说明：

1、这是答疑区碰到的一个公式，略做了调整；
2、核心有2处：
A、完全换手的区域：期间的平均成本和首日的最高价；
B、距今第三根阳、阴线的最高价；
3、突破动作；
4、排序：数字越小、表明近期参与者大量增加，爆发力可能更大，可优先选择；